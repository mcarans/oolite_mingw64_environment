# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=SDL
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2.15+e35cac7
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://libsdl.org"
msys2_references=(
  "cpe: cpe:/a:libsdl:libsdl"
  "cpe: cpe:/a:libsdl:simple_directmedia_layer"
)
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-autotools" "git")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs" "${MINGW_PACKAGE_PREFIX}-libiconv")
_commit=e711414dc34263f396d54183c52f9b3f7feecea9 # oolite_changes
source=("git+https://github.com/mcarans/oolite_SDL-1.2#commit=${_commit}")
sha256sums=('084cab20cc4446e4e84544eed02caa100e643bbcf52291a18087e260c8b880ba')

pkgver() {
  cd "$srcdir/$_gitname"
  # Use short hash
  printf "%s" "1.2.15+$(git rev-parse --short HEAD)"
}

prepare() {
  cd oolite_SDL-1.2
  ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../oolite_SDL-1.2/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-assembly \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  sed -e "s|-mwindows||g" -i "${pkgdir}"${MINGW_PREFIX}/lib/pkgconfig/sdl.pc
}
