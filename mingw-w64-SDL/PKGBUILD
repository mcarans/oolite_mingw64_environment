# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=SDL
_gitname=SDL-1.2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2.15+5b402cee
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://libsdl.org"
msys2_references=(
  "cpe: cpe:/a:libsdl:libsdl"
  "cpe: cpe:/a:libsdl:simple_directmedia_layer"
)
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-autotools" "git" "patch")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs" "${MINGW_PACKAGE_PREFIX}-libiconv")
_commit=5b402cee2da923942fcd4fad42aaebd1185c9c17
source=("git+https://github.com/libsdl-org/${_gitname}#commit=${_commit}"
        "Oolite_changes.patch")
sha256sums=('d7962c8ef1282c93d9147da3864a17f552799f8e85c574fadfa557c5f883e8f1'
            '8f96b0ead4a2f3de47160a3b77134e2bb23d456fe35b70ce72f20b547bbc8d82')

pkgver() {
  cd "${srcdir}/${_gitname}"
  # Use short hash
  printf "%s" "1.2.15+$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  patch -p1 -i ${srcdir}/Oolite_changes.patch
  ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  "../${_gitname}/configure" \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-assembly \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  sed -e "s|-mwindows||g" -i "${pkgdir}"${MINGW_PREFIX}/lib/pkgconfig/sdl.pc
}
